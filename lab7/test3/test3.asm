T1MOD_W	EQU	00100000B
			;GATE1=0
			;C/T1=0
			;M1=1,M0=0
BPS_I	EQU	0FDH
COMM	DATA	070H
FLAG	BIT		00H
A_LAMP	EQU		P2
E_G_D	EQU		01101010B
W_G_D	EQU		10100110B
S_G_D	EQU		10011010B
N_G_D	EQU		10101001B
		ORG		0000H
		LJMP	START
		ORG		0023H
		LJMP	RS232_S
		ORG		0030H
START:	LCALL	INIT
MLOOP:	CLR		FLAG
LOOP:	JNB		FLAG,LOOP
		MOV		A,COMM
		CJNE	A,#'1',N_C1
		MOV		A_LAMP,#E_G_D
		SJMP	MLOOP
N_C1:	CJNE	A,#'3',N_C2
		MOV		A_LAMP,#W_G_D
		SJMP	MLOOP
N_C2:	CJNE	A,#'2',N_C3
		MOV		A_LAMP,#S_G_D
		SJMP	MLOOP
N_C3:	MOV		A_LAMP,#N_G_D
		SJMP	MLOOP
INIT:	SETB	SM0
		SETB	SM1
		SETB	REN
		MOV		TMOD,#T1MOD_W
		MOV		TH1,#BPS_I
		MOV		TL1,#BPS_I
		SETB	TR1
		SETB	ES
		SETB	EA
		RET
RS232_S:
		JNB		TI,RECE
		CLR		TI
		RETI
RECE:	MOV		A,SBUF
		JNB		P,P_Z
		JNB		RB8,ERROR
		SJMP	RIGHT
P_Z:	JB		RB8,ERROR
RIGHT:	MOV		COMM,A
		MOV		A,#'Y'
		MOV		SBUF,A
		SETB	FLAG
		CLR		RI
		RETI
ERROR:	MOV		A,#'N'
		MOV		SBUF,A
		CLR		FLAG
		CLR		RI
		RETI
		END